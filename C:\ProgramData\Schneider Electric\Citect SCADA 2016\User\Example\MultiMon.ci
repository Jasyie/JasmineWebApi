//-----------------------------------------------------------------------------
//	Copyright © 2015 Schneider Electric (Australia) Pty Ltd. All rights reserved.
//
// 	FILE:		MulltiMon.ci
//
//	CREATED:	30/10/2009
//
//	SUMMARY:
//
//		Contains support functions for the Multi-monitor Overview in Example project.
//
//	PUBLIC FUNCTIONS:
//
//		MM_Move
//		MM_ParameterGet
//		MM_Restart
//		MM_Scale
//
//-----------------------------------------------------------------------------


//-----------------------------------------------------------------------------
//
//	Constants
//
//-----------------------------------------------------------------------------

STRING	c_MM_PARAM_NOT_INITIALISED = "<N/A>";


//-----------------------------------------------------------------------------
//
//	Module Variables (for caching INI parameters)
//
//-----------------------------------------------------------------------------

STRING	m_MM_Param_Monitors 	= "<N/A>";
STRING	m_MM_Param_StartupPage[8]	= "<N/A>", "<N/A>", "<N/A>", "<N/A>", "<N/A>", "<N/A>", "<N/A>", "<N/A>";


//-----------------------------------------------------------------------------
//
//	Public Functions
//
//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------
//
//	FUNCTION NAME:
//		MM_ParameterGet
//
//	SUMMARY:
//		Get Citect.INI parameter from [MultiMonitors] section
//			
//	PARAMS:
//		id	- string ID of the INI entry
//			
//	RETURNS: 
//		Citect.INI entry value
//
//-----------------------------------------------------------------------------
STRING FUNCTION MM_ParameterGet(STRING id)
	INT monitorIndex;

	IF ("Monitors" = id) THEN
		IF (c_MM_PARAM_NOT_INITIALISED = m_MM_Param_Monitors) THEN
			m_MM_Param_Monitors = ParameterGet("MultiMonitors", id, "");
		END
		RETURN m_MM_Param_Monitors;
	ELSE
		// assume id = StartupPage#
		monitorIndex = StrToInt(StrRight(id, 1)) - 1;
		IF ((monitorIndex < 0) OR (monitorIndex >= 8)) THEN
			RETURN ParameterGet("MultiMonitors", id, "");
		END
			
		IF (c_MM_PARAM_NOT_INITIALISED = m_MM_Param_StartupPage[monitorIndex]) THEN
			m_MM_Param_StartupPage[monitorIndex] = ParameterGet("MultiMonitors", id, "");
		END
		RETURN m_MM_Param_StartupPage[monitorIndex];
	END
END

//-----------------------------------------------------------------------------
//
//	FUNCTION NAME:
//		MM_Move
//
//	SUMMARY:
//		Position the monitor dynamic object in the screen block
//			
//	PARAMS:
//		monitor		OS identify number of the monitor, 0 is the virtual screen
//		direction	code for direction: 0 for horizontal, 1 for vertical
//			
//	RETURNS: 
//		Ratio of moving in the range of [-0.5, 0.5]
//
//-----------------------------------------------------------------------------
REAL FUNCTION MM_Move(INT monitor, INT direction)
	REAL	vsSizeMax 	= Max(WndMonitorInfo(0,2), WndMonitorInfo(0,3));
	REAL	monCenter 	= (WndMonitorInfo(monitor, direction)*2+WndMonitorInfo(monitor, direction + 2))/2;
	REAL	vsCenter 	= (WndMonitorInfo(0,       direction)*2+WndMonitorInfo(0,       direction + 2))/2;
	RETURN (monCenter - vsCenter) / vsSizeMax;
END

//-----------------------------------------------------------------------------
//
//	FUNCTION NAME:
//		MM_Scale
//
//	SUMMARY:
//		Resize the monitor dynamic object in the screen block
//			
//	PARAMS:
//		monitor		OS identify number of the monitor, 0 is the virtual screen
//		direction	code for direction: 0 for horizontal, 1 for vertical
//			
//	RETURNS: 
//		Ratio of scaling in the range of [0.0, 1.0]
//
//-----------------------------------------------------------------------------
REAL FUNCTION MM_Scale(INT monitor, INT direction)
	RETURN WndMonitorInfo(monitor, direction + 2)/Max(WndMonitorInfo(0,2),WndMonitorInfo(0,3));
END

//-----------------------------------------------------------------------------
//
//	FUNCTION NAME:
//		MM_Restart
//
//	SUMMARY:
//		Restart the Client with given number of monitors with preset start page
//			
//	PARAMS:
//		n			Number of monitors
//			
//	RETURNS: 
//		N/A
//
//-----------------------------------------------------------------------------
FUNCTION MM_Restart(INT n)
	INT i;
	INT monitors = WndInfo(80);
	STRING startupPage = PageInfo(0);

	ParameterPut("MultiMonitors", "Monitors", n);
	ParameterPut("MultiMonitors", "StartupPage1", "");
	FOR i = 2 TO n DO
		ParameterPut("MultiMonitors", "StartupPage" + IntToStr(i), startupPage);
	END
	FOR i = n + 1 TO monitors DO
		IF ParameterGet("MultiMonitors", "StartupPage" + IntToStr(i), "") <> "" THEN
			ParameterPut("MultiMonitors", "StartupPage" + IntToStr(i), "");
		END
	END
	RestartForm();
END
