
!	Multimedia Functions & Sequences


//	PlayWav

! 	sFile 	name of wav file (stored in project directory)
!	FromMs	Millisecond offset to start playing from	
!	ToMs	Millisecond offset to stop playing
!	Mode	0 play part of track before returning
!		1 play part of track and return immediately
!		2 play whole track before returning
!		3 play whole track and return immediately

String
FUNCTION	PlayWav(String sFile, String FromMs="", String ToMs="", Int Mode=0)

String 	sPath;
String 	sCommand;
String 	sError;
String 	Status;
INT	counter=0;
	
	sPath = "[RUN]:" + sFile + ".wav";
	sPath = PathToStr(sPath);	
	sCommand = "Open ^"" + sPath + "^" Alias WAV type WaveAudio";
	sError=	DspMCI(sCommand);

	While StrLeft(sError,11) = "MMSYSTEM289" AND (Counter < 3000) DO
		sleep(1);
		sError=	DspMCI(sCommand);
		counter = counter + 1;
	END

	DspMCI("Set WAV time format milliseconds");

	sCommand =  "Play WAV";		! Mode 2 or 3 Play whole Track

	IF Mode = 0 or (MODE = 1) THEN	! Play Part of Track
		sCommand = sCommand + " From "+FromMs+" To "+ToMs;
	END
	
	sError=DspMCI(sCommand);	! Play Sound

	If StrLength(sError) > 1 THEN		!If Error then send to kernel window
		traceMSG(sCommand); traceMsg(sError);
	END

	IF (Mode = 0) or (Mode=2) THEN		!Check for completion
		Status = StrLower(DspMCI("Status WAV Mode"));
		While (Status = "playing") or (Status = "seeking") DO
			SleepMS(100);	
			Status = StrLower(DspMCI("Status WAV Mode"));
		END 
		DspMCI("Close WAV");
	END

	IF Mode = 1 THEN
		TaskNew("CloseWav","",0);	!start new thread to check for completion
	END
	IF Mode = 3 THEN
		TaskNew("CloseWav","",0);
	END
	Return (sError);
END

FUNCTION
CloseWav()
String Status;
	Status = StrLower(DspMCI("Status WAV Mode"));
	While (Status = "playing") or (Status = "seeking") DO
		Sleepms(100);	
		Status = StrLower(DspMCI("Status WAV Mode"));
	END 
	DspMCI("Close WAV");
END


